#!/usr/bin/awk -f
# tbgen: generate testbench from verilog module
# tbgen is in public domain

BEGIN {
	# variables settable by -v
	if (!timeunit)  timeunit = "ns"
	if (!tbname)    tbname = "testbench"
	if (!vcdfile)   vcdfile = "testbench.vcd"
	if (!dumplevel) dumplevel = 1
	if (!timescale) timescale = 1
	if (!duration)  duration = 100
	if (!step)      step = 1
	if (!module)    module = ""
	if (!clock)     clock = "clk"
	if (!reset)     reset = "rst"
	if (!incdir)    incdir = ""
	if (!dumphex)   dumphex = 1

	# internal variables
	progname = ARGV[0]
	nsteps = duration / step
	useclock = 0
	usereset = 0
	initialpause = 0

	for (i = 1; ARGV[i] ~ /^[A-Za-z_][A-Za-z0-9_]*:/ ; i++) {
		n = split(ARGV[i], a, ":")
		if (a[1] == "dump") {
			if (n != 3)
				err("invalid argument: " ARGV[i])
			dump[a[2]] = a[3]
		} else if (n == 2) {
			inputvalue[a[1]] = a[2]
			inputstep[a[1]] = 1
		} else if (n == 3) {
			inputvalue[a[1]] = a[2]
			inputstep[a[1]] = a[3]
		} else {
			err("invalid argument: " ARGV[i])
		}
		ARGV[i] = ""
	}
	if (i >= ARGC)          # no file names so force stdin
		ARGV[ARGC++] = "-"

	srand()
}

function err(str) {
	printf "%s: %s\n", progname, str >"/dev/stderr"
	error = 1
	exit
}

function abs(n) {
	return n < 0 ? -n : n;
}

function callbc(str, res) {
	cmd = sprintf("echo %c%s%c | bc", 39, str, 39)
	cmd | getline res
	close(cmd)
	return res
}

function getsizestr(str, i, s, n, a, b) {
	for (i in parameters)
		gsub(i, parameters[i], str)
	gsub("[*][*]", "^", str)

	n = split(str, s, "[]:[]")
	if (n == 3) {
		a = callbc(s[2])
		return "[" a "]"
	} else {
		a = callbc(s[2])
		b = callbc(s[3])
		return "[" a ":" b "]"
	}
	err("could not compute size of signal bus: " sizestring)
}

function getsizelen(sizestring, len, a) {
	if (sizestring == " ")
		return 1

	len = split(sizestring, a, "[]:[]")
	if (len == 3) {
		return a[2]
	} else if (len == 4) {
		return abs(a[2] - a[3] + 1)
	}
	err("could not compute size of signal bus: " sizestring)
}

function randombits(n, i) {
	printf "%s'b", n
	for (i = 0; i < n; i++) {
		if (rand() < 0.5)
			printf "0"
		else
			printf "1"
	}
}

function increasing(n, i) {
	if (resetcount) {
		Incmax = 1
		for (i = 0; i < n; i++)
			Incmax *= 2
		Incval = 0
		resetcount = 0
	}

	printf "%s'h%x", n, Incval
	Incval = (Incval + 1) % Incmax
}

function decreasing(n, i) {
	if (resetcount) {
		Incmax = 1
		for (i = 0; i < n; i++)
			Incmax *= 2
		Incval = Incmax - 1
		resetcount = 0
	}

	printf "%s'h%x", n, Incval
	if (!Incval)
		Incval = Incmax - 1
	else
		Incval = (Incval - 1) % Incmax
}

function header() {
	printf "`timescale %s %s / %s %s", timescale, timeunit, timescale, timeunit
	print ""
	print "module " tbname "();"
}

function signals(i) {
	if (usereset)
		printf "\treg %s;\n", reset
	if (useclock)
		printf "\treg %s;\n", clock
	for (i in inputs)
		printf "\treg %s %s;\n", inputs[i], i
	for (i in outputs)
		printf "\twire %s %s;\n", outputs[i], i
	print ""
}

function dut(i, a, n) {
	n = 0

	for (i in inputs)
		a[n++] = i
	for (i in outputs)
		a[n++] = i

	printf "\t%s dut(", module
	if (usereset)
		printf ".%s(%s)%s", reset, reset, (useclock || i) ? ", " : ""
	if (useclock)
		printf ".%s(%s)%s", clock, clock, (i) ? ", " : ""
	for (i = 0; i < n; i++)
		printf ".%s(%s)%s", a[i], a[i], (i == n - 1) ? "" : ", "
	printf ");\n\n"
}

function initial() {
	printf "\tinitial\n"
	printf "\tbegin\n"
	printf "\t\t$dumpfile(\"%s\");\n", vcdfile
	printf "\t\t$dumpvars(%d, %s);\n", dumplevel, tbname
	printf "\t\t#%s;\n", duration
	for (reg in dump)
		printf "\t\t$writemem%s(\"%s\", dut.%s);\n", (dumphex ? "h" : "b"), dump[reg], reg
	printf "\t\t$finish;\n"
	printf "\tend\n"
}

function genreset() {
	initialpause = 2

	printf "\n"
	printf "\t/* %s */\n", reset
	printf "\tinitial\n"
	printf "\tbegin\n"
	printf "\t\t%s = 1'b1;\n", reset
	printf "\t\t#2; %s = %s;\n", reset, "~" reset
	printf "\tend\n"
}

function genclock(i) {
	resetcount = 1

	printf "\n"
	printf "\t/* %s */\n", reset
	printf "\tinitial\n"
	printf "\tbegin\n"
	for (i = 0; i < nsteps; i++) {
		printf "\t\t"
		if (i)
			printf "#%d; ", step
		printf "%s = ", clock
		decreasing(1)
		printf ";\n"
	}
	printf "\tend\n"
}

function geninput(input, sizelen) {
	resetcount = 1

	split(input, a, ";")

	printf "\n"
	printf "\t/* %s */\n", a[1]
	printf "\tinitial\n"
	printf "\tbegin\n"

	sizelen = getsizelen(inputs[input])
	if (inputvalue[input] == "r") {
		for (i = initialpause; i < nsteps / inputstep[input]; i++) {
			printf "\t\t"
			printf "#%s; ", step * inputstep[input]
			printf "%s = ", input
			randombits(sizelen)
			printf ";\n"
		}
	} else if (inputvalue[input] == "i") {
		for (i = initialpause; i < nsteps / inputstep[input]; i++) {
			printf "\t\t"
			printf "#%s; ", step * inputstep[input]
			printf "%s = ", input
			increasing(sizelen)
			printf ";\n"
		}
	} else if (inputvalue[input] == "d") {
		for (i = initialpause; i < nsteps / inputstep[input]; i++) {
			printf "\t\t"
			printf "#%s; ", step * inputstep[input]
			printf "%s = ", input
			decreasing(sizelen)
			printf ";\n"
		}
	} else {
		printf "\t\t%s = %s;\n", input, inputvalue[input]
	}

	printf "\tend\n"
}

function footer() {
	print "endmodule"
}

function parameter(str) {
	sub(".*parameter *", "", str)
	gsub("[,=;]", "", str)
	gsub("  *", " ", str)

	n = split(str, a)
	for (i = 1; i <= n; i++) {
		key = a[i]
		parameters[key] = a[++i]
	}
}

function define(str) {
	gsub("[ \t]+", " ", str)
	n = split(str, a)
	key = a[2]
	for (i = 3; i <= n; i++) {
		parameters[key] = parameters[key] a[i]
	}
}

# remove single-line comments
{
	sub(/\/\/.*/, "")
	sub(/\/\*.*\*\//, "")
}

# remove multi-line comments
/\/\*/ {
	comment = 2
	sub(/\/\*.*/, "")
}

# remove multi-line comments
/\*\// {
	comment = 0
	sub(/.*\*\//, "")
}

# remove multi-line comments
comment {
	if (comment == 1)
		next
	else
		comment = 1
}

/`include/ {
	file = $2
	sub("^\"", "", file)
	sub("\"$", "", file)
	success = getline <file
	if (success == -1)
		file = incdir "/" file
	if (incdir)
		success = getline <file
	while (success == 1) {
		gsub("/\\*.*\\*/", "", str)
		if ($1 == "parameter") {
			parameter($0)
		} else if ($1 == "`define") {
			define($0)
		}
		success = getline <file
	}
	close(file)
	next
}

/^module/ {
	if (module == "") {
		split($0, a, "[ (]")
		module = a[2]
	}
	next
}

/input/ {
	sub(".*input +(wire|reg)? +(unsigned|signed)? *", "", $0)   # remove keywords from $0
	gsub("[,\(\);]", " ", $0)               # remove separators from $0
	gsub("]", "] ", $0)

	n = split($0, a)
	sizestring = " "
	for (i = 1; i <= n; i++) {
		if (match(a[i], "^[[].*")) {
			while (!match(a[i], ".*[]]")) {
				s = a[i++];
				a[i] = s a[i];
			}
		}
		if (match(a[i], "\[[^:]*:?[^\]]*\]"))
			sizestring = getsizestr(a[i++])
		if (a[i] == clock) {
			useclock = 1
			break;
		} else if (a[i] == reset) {
			usereset = 1
			break;
		}
		inputs[a[i]] = sizestring
		if (!inputvalue[a[i]])
			inputvalue[a[i]] = "r"
		if (!inputstep[a[i]])
			inputstep[a[i]] = 1
	}
	next
}

/output/ {
	sub(".*output +(wire|reg)? +(unsigned|signed)? *", "", $0)  # remove keywords from $0
	gsub("[,\(\);]", "", $0)                # remove separators from $0
	gsub("]", "] ", $0)

	n = split($0, a)
	sizestring = " "
	for (i = 1; i <= n; i++) {
		if (match(a[i], "^[[].*")) {
			while (!match(a[i], ".*[]]")) {
				s = a[i++];
				a[i] = s a[i];
			}
		}
		if (match(a[i], "\[[^:]*:?[^\]]*\]"))
			sizestring = getsizestr(a[i++])
		outputs[a[i]] = sizestring
	}
	next
}

/parameter/ {
	parameter($0)
	next
}

/endmodule/ {
	exit
}

END {
	if (error)
		exit 1

	header()
	signals()
	dut()
	initial()
	if (usereset)
		genreset()
	if (useclock)
		genclock()
	for (input in inputs)
		geninput(input)
	footer()
}
